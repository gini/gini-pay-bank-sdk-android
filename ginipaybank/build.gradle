plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.hiya.jacoco-android'
    id 'org.jetbrains.dokka'
}

jacoco {
    toolVersion = "0.8.3"
}

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode libVersionCode
        versionName libVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField 'int', 'VERSION_CODE', "$libVersionCode"
        buildConfigField 'String', 'VERSION_NAME', "\"$libVersionName\""
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

repositories {
    maven {
        url 'https://repo.gini.net/nexus/content/repositories/open'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"

    api "net.gini:gini-capture-sdk:${versions.capture}"
    api "net.gini:gini-capture-network-lib:${versions.capture}"
    api "net.gini:gini-capture-accounting-network-lib:${versions.capture}"
    api "net.gini:gini-pay-api-lib-android:${versions.payapi}"

    implementation "androidx.core:core-ktx:${versions.core}"
    implementation "androidx.activity:activity-ktx:${versions.activity}"
    implementation "androidx.fragment:fragment-ktx:${versions.fragment}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${versions.lifecycle}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerview}"
    implementation "com.google.android.material:material:${versions.material}"

    dokkaHtmlPlugin "org.jetbrains.dokka:kotlin-as-java-plugin:${versions.dokka}"

    testImplementation "junit:junit:${versions.junit}"
    androidTestImplementation "androidx.test.ext:junit:${versions.androidxTestJunit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.androidxTestEspresso}"
}


apply from: rootProject.file('gradle/codequality.gradle')
apply from: rootProject.file('gradle/maven.gradle')
apply from: rootProject.file('gradle/multidex_for_tests.gradle')

dokkaHtml {
    outputDirectory.set(file("$buildDir/dokka"))
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)

            includes.from(files("module.md", packageDocumentationPaths(android.sourceSets.main.java.srcDirs[0])))
        }
    }
}

dokkaJavadoc {
    outputDirectory.set(file("$buildDir/dokka-javadoc"))
}

static def packageDocumentationPaths(srcDir) {
    def packageDocs = []
    srcDir.eachFileRecurse {
        if (it.name == 'package.md') {
            packageDocs.add(it.getAbsolutePath())
        }
    }
    return packageDocs
}

task sourcesJar(type: Jar, dependsOn: 'assembleRelease') {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply plugin: 'maven'

// In order to upload to the repo, you can create a file ~/.gradle/gradle.properties with
//
// repoUser=<your user name>
// repoPassword=<your password>
// mavenSnapshotsRepoUrl=<url for snapshots>
// mavenRepoUrl=<url for releases>
// mavenLocalRepoUrl=<url for local releases>
uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = groupId
            pom.artifactId = artifactId
            pom.version = version
            if (project.hasProperty('devVersionSuffix')) {
                pom.version = "${pom.version}.${devVersionSuffix}"
            }
            if (project.hasProperty('mavenSnapshotsRepoUrl')) {
                pom.version = "${pom.version}-SNAPSHOT"
            }

            pom.project {
                licenses {
                    license {
                        name 'Private License'
                        url 'https://raw.githubusercontent.com/gini/gini-pay-bank-sdk-android/master/LICENSE.md'
                        distribution 'repo'
                    }
                }
                organization {
                    name 'Gini GmbH'
                    url 'https://www.gini.net/'
                }
            }

            pom.whenConfigured { pom ->
                pom.dependencies.forEach { dep ->
                    if (dep.getArtifactId().startsWith("gini-")) {
                        dep.setGroupId(groupId)
                    }
                }
            }

            if (project.hasProperty('mavenSnapshotsRepoUrl')) {
                repository(url: mavenSnapshotsRepoUrl) {
                    authentication(
                            userName: project.hasProperty('repoUser') ? repoUser : "invalidUserName",
                            password: project.hasProperty('repoPassword') ? repoPassword : "invalidPassword"
                    )
                }
            }

            if (project.hasProperty('mavenRepoUrl')) {
                repository(url: mavenRepoUrl) {
                    authentication(
                            userName: project.hasProperty('repoUser') ? repoUser : 'invalidUserName',
                            password: project.hasProperty('repoPassword') ? repoPassword : 'invalidPassword'
                    )
                }
            }

            if (project.hasProperty('mavenLocalRepoUrl')) {
                repository(url: mavenLocalRepoUrl) {
                    authentication(
                            userName: project.hasProperty('repoUser') ? repoUser : 'invalidUserName',
                            password: project.hasProperty('repoPassword') ? repoPassword : 'invalidPassword'
                    )
                }
            }
        }
    }
}
